buildscript {
	repositories {
		mavenCentral()
		jcenter()
	}
	dependencies {
		classpath 'edu.sc.seis.gradle:launch4j:2.4.2'
	}
}

plugins {
	id 'org.jetbrains.kotlin.jvm' version '1.2.60'
	id 'com.github.johnrengelman.shadow' version '2.0.1'
	id 'com.palantir.docker' version '0.17.2'
	id 'java'
	id 'application'
}

apply plugin: 'com.palantir.docker'

// For building windows executable. See https://github.com/TheBoegl/gradle-launch4j
apply plugin: 'edu.sc.seis.launch4j'

group = 'com.symmetrylabs'
version = '1.0'

mainClassName = 'com.symmetrylabs.slstudio.SLStudio'
//mainClassName = 'com.symmetrylabs.slstudio.SLHeadless'

run {
	if (JavaVersion.current() == JavaVersion.VERSION_1_8) {
		jvmArgs '-XX:+UseParNewGC', '-XX:+UseConcMarkSweepGC'
	}

	// To select a layout from the command line, "gradlew run -Playout=cubes"
	systemProperty 'com.symmetrylabs.layout', findProperty('layout')
	// To start SLStudio with Live turned off, "gradlew run -PstartMuted=true"
	systemProperty 'com.symmetrylabs.startMuted', findProperty('startMuted')

	doLast {
		while (file(".restart").exists()) {
			file(".restart").delete()
			println 'Restarting...'
			run.exec()
		}
	}
}

docker {
	name 'symmetrylabs/' + project.name.toLowerCase() + ':latest'
	tags version
	dockerfile project.file('docker/Dockerfile')

	files tasks.distTar.outputs, project.files('docker'), project.files('.')
	copySpec.exclude 'build'

	if (System.getProperty("buildArgs") != null) {
		buildArgs System.getProperty("buildArgs").split(/\s+/).collectEntries {
			def kvp = it.split('=')
			[(kvp[0]): kvp[1]]
		}
	}
}

allprojects {
	repositories {
		mavenCentral()

		maven {
			url 'https://jitpack.io'
			// these credentials are for the symmetry-labs account
			credentials { username 'jp_ad5p43bkk3kiaah4slmihuagci' }
		}
	}

	tasks.withType(JavaCompile) {
		sourceCompatibility = '1.8'
	}
}

if (hasProperty('localp3lx')) {
	configurations.all {
		resolutionStrategy.dependencySubstitution {
			// when this is runs, the build uses ../P3LX and ../LX instead of the
			// jitpack dependency specified below
			substitute module("com.github.SymmetryLabs.p3lx:P3LX") with project(":P3LX")
			substitute module("com.github.SymmetryLabs.p3lx:LX") with project(":LX")
		}
	}
}

repositories {
	// needed for aparapi dependency
	maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
}

dependencies {
	// https://mvnrepository.com/artifact/org.processing/core
	implementation group: 'org.processing', name: 'core', version: '3.3.6'

	// https://mvnrepository.com/artifact/org.processing/video
	implementation group: 'org.processing', name: 'video', version: '3.2.3'

	// https://mvnrepository.com/artifact/com.harium/kdtree
	implementation group: 'com.harium', name: 'kdtree', version: '1.0.0'

	// https://mvnrepository.com/artifact/org.javassist/javassist
	implementation group: 'org.javassist', name: 'javassist', version: '3.22.0-GA'

	// https://mvnrepository.com/artifact/com.google.guava/guava
	implementation group: 'com.google.guava', name: 'guava', version: '23.5-jre'

	// https://mvnrepository.com/artifact/org.apache.commons/commons-math3
	implementation group: 'org.apache.commons', name: 'commons-math3', version: '3.6.1'

	// https://mvnrepository.com/artifact/org.jetbrains.kotlin/kotlin-stdlib
	implementation group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib', version: '1.2.60'

	// https://mvnrepository.com/artifact/io.github.lukehutch/fast-classpath-scanner
	implementation group: 'io.github.lukehutch', name: 'fast-classpath-scanner', version: '2.9.3'

	// lock aparapi-jni snapshot version
	// https://oss.sonatype.org/content/repositories/snapshots/com/aparapi/aparapi-jni/
	implementation "com.aparapi:aparapi-jni:1.1.3-20180211.063116-7"

	// https://github.com/SymmetryLabs/aparapi
	implementation 'com.github.SymmetryLabs:aparapi:b6fffad'

	implementation 'com.fazecast:jSerialComm:[2.0.0,3.0.0)'

	// http://mvnrepository.com/artifact/uk.co.caprica/vlcj
	// http://capricasoftware.co.uk/#/projects/vlcj
	implementation group: 'uk.co.caprica', name: 'vlcj', version: '3.10.1'

	// https://mvnrepository.com/artifact/org.slf4j/slf4j-simple/1.7.25
	// used for logging in vlcj, for more information about why this dependency
	// is declared here instead of vlcj (which is intentional!) check out
	// https://www.slf4j.org/manual.html
	implementation group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.25'

	implementation 'com.github.SymmetryLabs.p3lx:P3LX:dc9f0ae'

	implementation files('libs/minim.jar')
	implementation files('libs/jogamp-fat.jar')

	// https://mvnrepository.com/artifact/org.junit.jupiter/junit-jupiter-api
	testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.1.1'

	implementation group: 'de.javagl', name: 'obj', version: '0.3.0'

	testCompile 'junit:junit:4.12'
}

compileKotlin {
	kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
	kotlinOptions.jvmTarget = "1.8"
}

jar {
	exclude('**/*.swp')
	exclude('**/*.swo')
}
